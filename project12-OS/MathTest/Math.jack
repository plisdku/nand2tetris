class Math {

    static Array bits;

    function void init() {
        // Initialize the bitshift array
        // We can't multiply numbers or shift numbers in this function
        // because those functions need the bits array to work... ah well.
        //
        // But, WHO CALLS THIS FUNCTION????

        let bits = Array.new(16);

        let bits[0] = 1;
        let bits[1] = 2;
        let bits[2] = 4;
        let bits[3] = 8;
        let bits[4] = 16;
        let bits[5] = 32;
        let bits[6] = 64;
        let bits[7] = 128;
        let bits[8] = 256;
        let bits[9] = 512;
        let bits[10] = 1024;
        let bits[11] = 2048;
        let bits[12] = 4096;
        let bits[13] = 8192;
        let bits[14] = 16384;
        let bits[15] = 16384 + 16384; // 32768 is an illegal offset value

        return;
    }

    function int get_bit(int x, int i) {
        // Get the i-th bit of x

        var int out;
        let out = ~((x & bits[i]) = 0);
        return out;
    }


    function int multiply(int x, int y) {
        var int sum;
        var int shifted_x;
        var int i;

        let sum = 0;
        let i = 0;
        let shifted_x = x;

        while (i < 16) {
            if (Math.get_bit(y, i)) {
                let sum = sum + shifted_x;
            }
            let shifted_x = shifted_x + shifted_x;
            let i = i + 1;
        }

        return sum;
    }

    function int sign(int x) {
        if (x < 0) {
            return -1;
        }
        if (x > 0) {
            return 1;
        }
        return 0;
    }

    function int divide_positive(int x, int y) {
        // Divide positive integers

        var int q;

        if (y > x) {
            return 0;
        }
        
        let q = Math.divide_positive(x, 2*y);

        if ( (x - (2*q*y)) < y ) {
            return 2*q;
        } else {
            return (2*q) + 1;
        }
    }

    function int divide(int x, int y) {

        var int sign_x, sign_y, q;

        let sign_x = Math.sign(x);
        let sign_y = Math.sign(y);

        let q = Math.divide_positive(Math.abs(x), Math.abs(y));
        return q*sign_x*sign_y;
    }

    function int abs(int x) {
        if (x < 0) {
            return -x;
        }
        return x;
    }

    function int min(int x, int y) {
        if (x < y) {
            return x;
        }
        return y;
    }

    function int max(int x, int y) {
        if (x > y) {
            return x;
        }
        return y;
    }

    function int square(int x) {
        return x * x;
    }

    function int sqrt(int x) {
        var int y, j, n, squared_thing;

        let y = 0;
        let j = 7;

        while (~(j < 0)) {
            if (~(Math.square(y + bits[j]) > x)) {
                let y = y + bits[j];
            }
            let j = j - 1;
        }
        return y;
    }
}