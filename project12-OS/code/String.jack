/*
String
The String class represents strings of char values, and supports commonly needed string
processing services.
constructor String new(int maxLength): Constructs a new empty string with a maximum
length of maxLength and initial length of 0.
method void dispose(): Disposes this string.
method int length(): Returns the number of characters in this string.
method char charAt(int j): Returns the character at the j-th location of this string.
method void setCharAt(int j, char c): Sets the character at the j-th location of this string to c.
method String appendChar(char c): Appends c to this stringâ€™s end, and returns this string.
method void eraseLastChar(): Erases the last character from this string.
method int intValue(): Returns the integer value of this string, until a non-digit char is detected.
method void setInt(int val): Sets this string to hold a representation of the given value.
function char backSpace(): Returns the backspace character.
function char doubleQuote(): Returns the double quote (") character.
function char newLine(): Returns the newline character.
*/


class String {
    
    field int length;
    field int max_length;
    field Array arr;

    constructor String new(int in_max_length) {

        let arr = Array.new(in_max_length);
        let max_length = max_length;
        let length = 0;

        return this;
    }

    method void dispose() {
        do arr.dispose();
        return;
    }

    method int length() {
        return length;
    }

    method char charAt(int j) {
        return arr[j];
    }

    method void setCharAt(int j, char c) {
        let arr[j] = c;
        return;
    }

    method String appendChar(char c) {
        let arr[length] = c;
        let length = length + 1;
        return this;
    }

    method void eraseLastChar() {
        let length = length - 1;
        return;
    }

    method int intValue() {
        var int sign, idx, msd, place_value, total_value;
        let sign = 1;
        let msd = 0;
        let place_value = 1;

        // If starts with '-'
        if (arr[0] = 45) {
            let sign = -1;
            let msd = 1;
        }

        let idx = length-1;
        let total_value = 0;
        while (~(idx < msd)) {
            // 48 is code for 0
            let total_value = total_value + (place_value * (arr[idx] - 48));
            let idx = idx - 1;
            let place_value = place_value * 10;
        }

        return total_value * sign;
    }

    method void clear() {
        let length = 0;
        return;
    }

    method void setInt(int val) {

        var int idx, place_value, ones;
        var Array digits;

        do clear();

        let digits = Array.new(6); // seems enough

        if (val < 0) {
            do appendChar(45); // write minus sign
            let val = -val;
        }

        // From here on, val is a positive number!
        // Generate the digits in reverse order

        let idx = 0;
        while (val > 0) {
            let ones = val - ((val / 10)*10);
            let val = val / 10;
            let digits[idx] = ones;
            let idx = idx + 1;
        }
        let idx = idx - 1;

        // Now idx points to the last digit in the array.
        // Write into string in order

        while (~(idx < 0)) {
            do appendChar(48 + digits[idx]);
            let idx = idx - 1;
        }
        return;
    }

    function char backSpace() {
        return 129;
    }

    function char doubleQuote() {
        return 34;
    }

    function char newLine() {
        return 128;
    }

}