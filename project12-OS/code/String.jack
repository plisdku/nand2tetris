class String {
    
    field int length;
    field int max_length;
    field Array arr;

    constructor String new(int in_max_length) {

        let arr = Array.new(in_max_length);
        let max_length = in_max_length;
        let length = 0;

        return this;
    }

    method void dispose() {
        do arr.dispose();
        return;
    }

    method int length() {
        return length;
    }

    method char charAt(int j) {
        return arr[j];
    }

    method void setCharAt(int j, char c) {
        let arr[j] = c;
        return;
    }

    method String appendChar(char c) {
        let arr[length] = c;
        let length = length + 1;
        return this;
    }

    method void eraseLastChar() {
        let length = length - 1;
        return;
    }

    method int intValue() {
        var int intVal, index;
        var boolean neg;

        let intVal = 0;

        // check sign
        if ((length > 0) & (arr[0] = 45)) {
            let neg = true;
            let index = 1;
        }
        else {
            let neg = false;
            let index = 0;
        }

        // parse left-to-right until non-digit
        while ((index < length) & ~(arr[index] < 48) & ~(arr[index] > 57)) {
            let intVal = (intVal * 10) + (arr[index] - 48);
            let index = index + 1;
        }

        if (neg) {
            return -intVal;
        }
        return intVal;
    }

    method void clear() {
        let length = 0;
        return;
    }

    method void setInt(int val) {

        var int idx, place_value, ones;
        var Array digits;

        do clear();

        let digits = Array.new(6); // seems enough

        if (val < 0) {
            do appendChar(45); // write minus sign
            let val = -val;
        }

        // From here on, val is a positive number!
        // Generate the digits in reverse order

        let idx = 0;
        while (val > 0) {
            let ones = val - ((val / 10)*10);
            let val = val / 10;
            let digits[idx] = ones;
            let idx = idx + 1;
        }
        let idx = idx - 1;

        // Now idx points to the last digit in the array.
        // Write into string in order

        while (~(idx < 0)) {
            do appendChar(48 + digits[idx]);
            let idx = idx - 1;
        }
        return;
    }

    function char backSpace() {
        return 129;
    }

    function char doubleQuote() {
        return 34;
    }

    function char newLine() {
        return 128;
    }

}